plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0'
}

group = 'org.earlydreamland.keepinventory'
version = '1.0.3'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
}

configurations {
    shade
    compileOnly.extendsFrom shade
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.13.+'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

shadowJar {
    archiveBaseName = 'KeepInventory'
    archiveClassifier = ''

    minimize {
        exclude(dependency('org.spigotmc:spigot-api:.*'))
    }

    mergeServiceFiles()

    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
}

build {
    dependsOn shadowJar
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['plugin.yml', '*.properties']) {
        expand props
    }
}

test {
    useJUnitPlatform()
}